/**
 * Print three dimensions of accelerometer measurements on stdout.
 * To run this program on the <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>
 * and see the printed outputs, first put the robot in BOOTSEL mode (hold button B while
 * resetting). Then the sequence of commands is something like this:
 *
 * ```
 *     $ cd ~/lf-pico
 *     $ lfc src/AccelerometerPrintf.lf
 *     ...
 *     $ picotool load -x bin/AccelerometerPrintf.elf
 *     $ ls /dev/*usb*
 *     /dev/cu.usbmodem14201	/dev/tty.usbmodem14201
 *     $ screen /dev/cu.usbmodem14201 115200
 *     ...
 *     Control-A k
 * ```
 *
 * This compiles the program, loads it into flash memory on the robot, finds the USB
 * serial bus (which on this machine happens to be named `/dev/cu.usbmodem14201`),
 * and connects the `screen` terminal emulator to the robot through the USB serial port
 * with baud rate 115,200 bits per second.
 * 
 * @author Edward A. Lee
 */
 target C {
    platform: {
        name: "RP2040",
        board: "pico:usb"
    },
    threading: false,
};

import Accelerometer from "lib/Imu.lf";
main reactor {
    a = new Accelerometer();
    timer t(0, 250 msec);
    reaction(t) -> a.trigger {=
        lf_set(a.trigger, true);
    =}
    reaction(a.x, a.y, a.z) {=
        // Print results.
        printf("x: %f\ty: %f\tz:%f\n", a.x->value, a.y->value, a.z->value);
    =}
}