/**
 * @brief Library reactor for the LCD display on the the <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
 target C {
    platform: "RP2040",
    threading: false,
    files: ["../lib/"],                  // Needed to find display.h
    cmake-include: [
        "../../platform/robot_lib.txt",  // Needed to find display.h
        "../../platform/pico_config.txt" // Needed to find pico/stdlib.h
    ]
}

preamble {=
    #include <pico/stdlib.h>
    #include <display.h>
=}

reactor LogicalDisplay {
    // inputs 
    input line0:string;
    input line1:string;
    input line2:string;
    input line3:string;
    // buffer update action
    logical action update;
    reaction(startup) {=
        display_init();
        display_set_font(font_8x16);
    =}
    // TODO: maybe use a bank of subreactors?
    reaction(line0) -> update {=
        // clear line in buffer
        display_fill(0);
        display_text(line0->value, 0, 0, COLOR_WHITE | DISPLAY_NOW);
        lf_schedule(update, 0); 
    =}
    reaction(line1) -> update {=
        // clear line in buffer
        display_text(line1->value, 0, 16, COLOR_WHITE_ON_BLACK); 
        lf_schedule(update, 0); 
    =}
    reaction(line2) -> update {=
        // clear line in buffer
        display_text(line2->value, 0, 32, COLOR_WHITE_ON_BLACK);
        lf_schedule(update, 0); 
    =}
    reaction(line3) -> update {=
        // clear line in buffer
        display_text(line3->value, 0, 48, COLOR_WHITE_ON_BLACK); 
        lf_schedule(update, 0); 
    =}    
    reaction(update) {=
        // clear and update screen
        display_fill(0);
        display_show();
    =}
}

/**
 * Display up to four lines of text on the LCD display of the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 */
reactor Display {
  preamble {=
    static bool _pololu_robot_display_initialized = false;
  =}
  // inputs 
  input line0:string;
  input line1:string;
  input line2:string;
  input line3:string;
  // buffer update action
  reaction(startup) {=
    // Initialize only once if there are multiple instances.
    if (!_pololu_robot_display_initialized) {
      _pololu_robot_display_initialized = true;
      display_init();
      display_set_font(font_8x16);
    }
  =}
  reaction(line0, line1, line2, line3) {=
    // clear buffer
    display_fill(0);
    if (line0_is_present) display_text(line0->value, 0, 0, COLOR_WHITE);
    if (line1_is_present) display_text(line1->value, 0, 16, COLOR_WHITE); 
    if (line2_is_present) display_text(line2->value, 0, 32, COLOR_WHITE);
    if (line3_is_present) display_text(line3->value, 0, 48, COLOR_WHITE); 
    // display buffer
    display_show();
  =}
}
